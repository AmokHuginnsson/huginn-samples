#! /bin/sh
exec huginn --no-argv -E "${0}"
#! huginn

/*
 * Based on:
 * Author: Todd Larason <jtl@molehill.org>
 * $XFree86: xc/programs/xterm/vttests/256colors2.pl,v 1.2 2002/03/26 01:46:43 dickey Exp $
 *
 * use the resources for colors 0-15 - usually more-or-less a
 * reproduction of the standard ANSI colors, but possibly more
 * pleasing shades
 */

import Algorithms as algo;
import Terminal as term;

main() {
	// display the colors

	// first the system ones:
	print( "System colors:\n" );
	for ( color : [ term.COLOR.BLACK, term.COLOR.RED, term.COLOR.GREEN, term.COLOR.BROWN, term.COLOR.BLUE, term.COLOR.MAGENTA, term.COLOR.CYAN, term.COLOR.LIGHTGRAY ] ) {
		print( term.color( color, "██" ) );
	}
	print( "\n" );
	for ( color : [ term.COLOR.GRAY, term.COLOR.BRIGHTRED, term.COLOR.BRIGHTGREEN, term.COLOR.YELLOW, term.COLOR.BRIGHTBLUE, term.COLOR.BRIGHTMAGENTA, term.COLOR.BRIGHTCYAN, term.COLOR.WHITE ] ) {
		print( term.color( color, "██" ) );
	}
	print( "\n\n" );

	// now the color cube
	print( "Color cube, 6x6x6:\n" );
	for ( green : algo.range( 6 ) ) {
		for ( red : algo.range( 6 ) ) {
			for ( blue : algo.range( 6 ) ) {
				print( term.color256( red, green, blue, "██" ) );
			}
			print( " " );
		}
		print( "\n" );
	}


	// now the grayscale ramp
	print( "Grayscale ramp:\n" );
	for ( color : algo.range( 24 ) ) {
		print( term.gray_scale( color, "██" ) );
	}
	print( "\n" );
	return ( 0 );
}

